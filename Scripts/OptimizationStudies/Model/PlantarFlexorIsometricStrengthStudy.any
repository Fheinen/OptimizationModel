
AnyFolder Model = {
  // Model parts included in the moment arm study
  AnyFolder &PelvisRef = ......BodyModelRef.Trunk.SegmentsLumbar.PelvisSeg; //This is a reference to the folder which holds the human part of the model
  AnyFolder &LegSegSideRef = .SideBodyModelRef.Leg.Seg; //This is a reference to the folder which holds the human part of the model
  AnyFolder &LegJntsRef =  .SideBodyModelRef.Leg.Jnt;
  AnyFolder &MusRef = .SideBodyModelRef.Leg.Mus;
//  AnyFolder &LegMusRef = .SideBodyModelRef.Leg.Mus;
//  AnyFolder &SpineMusRef = .SideTrunkMusRef;
  
 AnyFixedRefFrame ground = {
   AnyRefNode node = { 
      ARel=..PelvisRef.Axes0;
     sRel=..PelvisRef.r0;
   };
 };
 

AnyFolder Drivers = {
   AnyVar AngularVelocity = (..RangeOfMotion[1]-..RangeOfMotion[0])*pi/180;
 
 AnyKinMeasureOrg LegMeasure = {
  AnyKinMeasure &HipFlexion= ...SideBodyInterfaceRef.HipFlexion;
  AnyKinMeasure &HipAbduction= ...SideBodyInterfaceRef.HipAbduction;
  AnyKinMeasure &HipExternalRotation= ...SideBodyInterfaceRef.HipExternalRotation;
  AnyKinMeasure &KneeFlexion= ...SideBodyInterfaceRef.KneeFlexion;
  AnyKinMeasure &AnklePlantarFlexion= ...SideBodyInterfaceRef.AnklePlantarFlexion;
  AnyKinMeasure &SubTalarEversion= ...SideBodyInterfaceRef.SubTalarEversion;  
 };
   
 AnyStdJoint PelvisGround={
    AnyRefFrame &Ground = ..ground.node;
    AnyRefFrame &Sacrum= ..PelvisRef; 
 };
    
};// Drivers
  AnyForce JointLoad = {
      AnyKinMeasure &ref = .Drivers.LegMeasure;
      AnyInt Sign = -round(.Drivers.AngularVelocity/abs(.Drivers.AngularVelocity));
      F = 50*Sign*not(not(.Drivers.LegDriver.Mask));//70 for max
  };

};
 
 
AnyBodyStudy Study = {
  
  #ifdef NO_GRAVITY
  Gravity = {0,0,0};
  #else
  Gravity = {0,-9.81,0};
  #endif
  tStart = 0;
  tEnd = 1;
  nStep = .nStep;
  InitialConditions.SolverType = KinSolDeterminate;
  Kinematics.SolverType = KinSolDeterminate;
  Kinematics.PosAnalysisOnlyOnOff = On;
  InitialConditions.PosAnalysisOnlyOnOff = On;
  InverseDynamics.Criterion.Type = MR_MinMaxStrict;
//  InverseDynamics.Criterion.Type = MR_Polynomial;
//  InverseDynamics.Criterion.Power = 2;

  AnyOutputFun JointStrengthOutput = 
    {
      Val = .JointStrength.JointStrength;
    };
  AnyOutputFun SoleusStrengthOutput = 
    {
      Val = .JointStrength.SoleusStrength;
    };
    
  #if RATIOANALYSISONOFF == 1  
  AnyString Name = strval(Main.Studies.EvaluateAnkleStrength.Right.IsometricStrength.AnklePlantarFlexion.RangeOfAngle);
  AnyOutputFile AnklePlantarRRatios = 
  {
    FileName = .Name+"AnklePlantarRRatios.txt";
    AnyMatrix RRatios = {{.JointStrength.RRatioSolMed1},{.JointStrength.RRatioSolMed2},{.JointStrength.RRatioSolMed3},
    {.JointStrength.RRatioSolLat1},{.JointStrength.RRatioSolLat2},{.JointStrength.RRatioSolLat3},
    {.JointStrength.RRatioGasMed},{.JointStrength.RRatioGasLat}};

  };
  #else
  #endif

   
  AnyFolder &Model = .Model;
  
  AnyFolder JointStrength = {
     AnyFolder Abscissa  ={
        AnyVar JointAngle = not(not(..Model.Drivers.LegDriver.DriverVel))*..Model.Drivers.LegMeasure.Pos'*180/pi;     
     };
     AnyVar JointStrength = max(abs(.Model.JointLoad.F))/(.MaxMuscleActivity+0.00000001);
     AnyVar SoleusStrength = max(abs(Main.HumanModel.BodyModel.Right.Leg.Mus.SoleusMedialis2.Strength));
     AnyVar RRatioSolMed1 = max((Main.HumanModel.BodyModel.Right.Leg.Mus.SoleusMedialis1.Lmt-Main.HumanModel.BodyModel.Right.Leg.MusPar.SoleusMedialis1Par.Lt0)/Main.HumanModel.BodyModel.Right.Leg.MusPar.SoleusMedialis1Par.Lfbar);
     AnyVar RRatioSolMed2 = max((Main.HumanModel.BodyModel.Right.Leg.Mus.SoleusMedialis2.Lmt-Main.HumanModel.BodyModel.Right.Leg.MusPar.SoleusMedialis2Par.Lt0)/Main.HumanModel.BodyModel.Right.Leg.MusPar.SoleusMedialis2Par.Lfbar);
     AnyVar RRatioSolMed3 = max((Main.HumanModel.BodyModel.Right.Leg.Mus.SoleusMedialis3.Lmt-Main.HumanModel.BodyModel.Right.Leg.MusPar.SoleusMedialis3Par.Lt0)/Main.HumanModel.BodyModel.Right.Leg.MusPar.SoleusMedialis3Par.Lfbar);

     AnyVar RRatioSolLat1 = max((Main.HumanModel.BodyModel.Right.Leg.Mus.SoleusLateralis1.Lmt-Main.HumanModel.BodyModel.Right.Leg.MusPar.SoleusLateralis1Par.Lt0)/Main.HumanModel.BodyModel.Right.Leg.MusPar.SoleusLateralis1Par.Lfbar);
     AnyVar RRatioSolLat2 = max((Main.HumanModel.BodyModel.Right.Leg.Mus.SoleusLateralis2.Lmt-Main.HumanModel.BodyModel.Right.Leg.MusPar.SoleusLateralis2Par.Lt0)/Main.HumanModel.BodyModel.Right.Leg.MusPar.SoleusLateralis2Par.Lfbar);
     AnyVar RRatioSolLat3 = max((Main.HumanModel.BodyModel.Right.Leg.Mus.SoleusLateralis3.Lmt-Main.HumanModel.BodyModel.Right.Leg.MusPar.SoleusLateralis3Par.Lt0)/Main.HumanModel.BodyModel.Right.Leg.MusPar.SoleusLateralis3Par.Lfbar);
     
     AnyVar RRatioGasMed = max((Main.HumanModel.BodyModel.Right.Leg.Mus.GastrocnemiusLateralis1.Lmt-Main.HumanModel.BodyModel.Right.Leg.MusPar.GastrocnemiusLateralis1Par.Lt0)/Main.HumanModel.BodyModel.Right.Leg.MusPar.GastrocnemiusLateralis1Par.Lfbar);
     AnyVar RRatioGasLat = max((Main.HumanModel.BodyModel.Right.Leg.Mus.GastrocnemiusMedialis1.Lmt-Main.HumanModel.BodyModel.Right.Leg.MusPar.GastrocnemiusMedialis1Par.Lt0)/Main.HumanModel.BodyModel.Right.Leg.MusPar.GastrocnemiusMedialis1Par.Lfbar);
     //AnyVar PsoasMajorT12I = -1*.Model.SpineMusRef.PsoasMajor.PMT12I_TM.LmtDot/.Model.Drivers.AngularVelocity;
  };
};

AnyOperationMacro MacroCmd = 
{

  MacroStr = {"operation " + CompleteNameOf(&.Study.InverseDynamics),
    "run"
#ifdef OUTPUT_SAVE_PATH
    ,
    "classoperation " + CompleteNameOf(&.Study.Output) + strquote("Save data") 
     + " --type=" + strquote("Deep") +" --file=" + strquote(
     OUTPUT_SAVE_PATH + .OutputFileName + ".anydata.h5")
#endif
   };
 };